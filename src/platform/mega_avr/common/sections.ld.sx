/*
 * CYANCORE LICENSE
 * Copyrights (C) 2019, Cyancore Team
 *
 * File Name		: sections.ld.sx
 * Description		: This file contains memory layout for the
 *			  cyancore framework
 * Primary Author	: Akash Kollipara [akashkollipara@gmail.com]
 * Organisation		: Cyancore Core-Team
 */

#include <plat_mem.h>

#ifndef L_MEM_START
#define L_MEM_START	0
#endif

MEMORY
{
	/* VM Addresses */
	vma_imem	: ORIGIN = V_IMEM_START, LENGTH = IMEM_LENGTH
	vma_dmem	: ORIGIN = V_DMEM_START, LENGTH = DMEM_LENGTH

	/* LM Addresses */
	lma_mem		: ORIGIN = L_MEM_START, LENGTH = IMEM_LENGTH
}

SECTIONS
{
	.text :
	{
		. = ALIGN(ALIGN_BOUND);
		*(.archvectors)
		KEEP(*(.archvectors))
		*(.platvectors)
		KEEP(*(.platvectors))
		. = ALIGN(ALIGN_BOUND);
		*(.text)
		*(.text.*)
		KEEP(*(.text))
	} > vma_imem AT > lma_mem

	.bss :
	{
		. = ALIGN(ALIGN_BOUND);
		*(.bss)
		*(.bss.*)
		KEEP(*(.bss))
		*(.sbss)
		*(.sbss.*)
		KEEP(*(.sbss))
		*(COMMON)
	} > vma_dmem

	.heap :
	{
		. = ALIGN(8);
		*(.heap)
		KEEP(*(.heap))
		. = . + HEAP_SIZE;
	} > vma_dmem

	.data :
	{
		. = ALIGN(ALIGN_BOUND);
		*(.version)
		KEEP(*(.version))
		*(.rodata)
		*(.rodata.*)
		KEEP(*(.rodata))
		*(.data)
		*(.data.*)
		KEEP(*(.data))
	} > vma_dmem AT > lma_mem

	.driver_table : {} > vma_dmem AT > lma_mem
	.mcall_table : {} > vma_dmem AT > lma_mem

	.stack :
	{
		. = ALIGN(ALIGN_BOUND);
		*(.stack)
		KEEP(*(.stack))
	} > vma_dmem AT > lma_mem

	PROVIDE(_text_start = ADDR(.text));
	PROVIDE(_text_size = SIZEOF(.text));
	PROVIDE(_text_end = _text_start + _text_size);

	PROVIDE(_data_start = LOADADDR(.data));
	PROVIDE(_data_size = SIZEOF(.data) + SIZEOF(.driver_table) + SIZEOF(.mcall_table));
	PROVIDE(_data_vstart = ADDR(.data));
	PROVIDE(_data_vend = _data_vstart + _data_size);

	PROVIDE(_bss_start = ADDR(.bss));
	PROVIDE(_bss_size = SIZEOF(.bss));
	PROVIDE(_bss_end = _bss_start + _bss_size);

	PROVIDE(_stack_start = ADDR(.stack));
	PROVIDE(_stack_size = SIZEOF(.stack));
	PROVIDE(_stack_end = _stack_start - _stack_size);

	PROVIDE(_heap_start = ADDR(.heap));
	PROVIDE(_heap_size = SIZEOF(.heap));
	PROVIDE(_heap_end = _heap_start + SIZEOF(.heap));

	PROVIDE(_flash_size = _data_size + SIZEOF(.text));
	PROVIDE(_ram_size = _bss_size + _data_size + SIZEOF(.stack) + SIZEOF(.heap));

	ASSERT((_flash_size < FLASH_SIZE), "< x > Flash size exceeded ...")
	ASSERT((_ram_size < RAM_SIZE), "< x > RAM size exceeded ...")

	/DISCARD/ : { *(.comment .trampolines) }
}
