/*
 * CYANCORE LICENSE
 * Copyrights (C) 2019, Cyancore Team
 *
 * File Name		: sections.ld.sx
 * Description		: This file contains memory layout for the
 *			  cyancore framework
 * Primary Author	: Akash Kollipara [akashkollipara@gmail.com]
 * Organisation		: Cyancore Core-Team
 */

#include <plat_mem.h>

#ifndef L_MEM_START
#define L_MEM_START	0
#endif

MEMORY
{
	/* VM Addresses */
	vma_itmem (airwx)	: ORIGIN = V_ITMEM_START, LENGTH = ITMEM_LENGTH
	vma_imem (arw!xi)	: ORIGIN = V_IMEM_START, LENGTH = IMEM_LENGTH
	vma_dmem (irx!aw)	: ORIGIN = V_DMEM_START, LENGTH = DMEM_LENGTH

	/* LM Addresses */
	lma_mem			: ORIGIN = L_MEM_START, LENGTH = L_MEM_LENGTH
}

ENTRY(entry)

SECTIONS
{
	.text :
	{
		KEEP(*(.text.entry))
		*(.text)
		*(.text.*)
		KEEP(*(.text))
	} > vma_imem AT > lma_mem

	.rodata :
	{
		. = ALIGN(8);
		*(.version)
		KEEP(*(.version))
		*(.rodata)
		*(.rodata.*)
		KEEP(*(.rodata))
	} > vma_imem AT > lma_mem

	.driver_table : {} > vma_imem AT > lma_mem
	.mcall_table : {} > vma_imem AT > lma_mem

	.data :
	{
		*(.data)
		*(.data.*)
		KEEP(*(.data))
	} > vma_dmem AT > lma_mem

	.tdata :
	{
		*(.tdata)
		*(.tdata.*)
		KEEP(*(.tdata))
	} > vma_dmem AT > lma_mem

	.static_global_var :
	{
	} > vma_dmem

	.static_data_global :
	{
		*(.sdata)
		*(.sdata.*)
		KEEP(*(.sdata))
	} > vma_dmem AT > lma_mem

	.static_bss_global :
	{
		*(.sbss)
		*(.sbss.*)
		KEEP(*(.sbss))
	} > vma_dmem

	.bss :
	{
		*(.bss)
		*(.bss.*)
		KEEP(*(.bss))
		*(COMMON)
	} > vma_dmem

	.stack :
	{
		*(.stack)
		KEEP(*(.stack))
	} > vma_dmem

	PROVIDE(_data_start = LOADADDR(.data));
	PROVIDE(_data_size = SIZEOF(.data) + SIZEOF(.driver_table) + SIZEOF(.mcall_table));
	PROVIDE(_data_vstart = ADDR(.data));
	PROVIDE(_data_vend = _data_vstart + _data_size);

	PROVIDE(_tls_start = ADDR(.tdata));
	PROVIDE(_global_start = ADDR(.static_global_var) + 0x800);
	PROVIDE(_stack_start = ADDR(.stack) + STACK_SIZE);

	PROVIDE(_bss_start = ADDR(.bss));
	PROVIDE(_bss_size = SIZEOF(.bss));
	PROVIDE(_flash_size = _data_size + SIZEOF(.text));
	PROVIDE(_ram_size = _bss_size + _data_size + SIZEOF(.stack));

	ASSERT((_flash_size < FLASH_SIZE), "< x > Flash size exceeded ...")
	ASSERT((_ram_size < RAM_SIZE), "< x > RAM size exceeded ...")

	/DISCARD/ : { *(.comment .trampolines) }
}
